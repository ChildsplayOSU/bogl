game ConnectFour

type Player = {A, B}
type Position = (Int, Int)

-- Board and input type definitions
--
type Board = Array (6,7) of {A, B, Empty}
type Input = Int

type Result = Player & {Tie}

-- grabs a position from a triple
grabPos : (Board, Int, Int) -> Position
grabPos(b,column,row) = (column, row)

-- returns the connect 4 position based on contents of a column
dropPiece : (Board, Int, Int) -> (Board, Int, Int)
dropPiece(b,column,count) = while not(b ! (column,count) == Empty)
    do dropPiece(b,column,count - 1)

-- We need to convert column input into positions
--
col2pos : (Board,Int) -> (Int,Int)
col2pos(b,c) = grabPos(dropPiece(b,c,7))

-- Game setup
--
initialBoard : Board
initialBoard!(x,y)= Empty

goFirst : Player
goFirst = A

nextAB : Player -> Player
nextAB(p) = if p == A then B else A

fourInARow : Board -> Bool
fourInARow(b) = or(inARow(4,A,b),inARow(4,B,b))

-- Game ending: game-over condition and game outcome
--
gameOver : Board -> Bool
gameOver(b) = or(fourInARow(b),isFull(b))

outcome : (Player, Board) -> Result
outcome(p,b) = if inARow(4,A,b) then A else
               if inARow(4,B,b) then B else Tie

-- Predefined operations
--
isValid : (Board, Position) -> Bool
isValid(b,p) = if b!p == Empty then True else False

-- Game loop
--
tryMove : (Player, Board) -> (Player, Board)
tryMove(p,b) = let pos = col2pos(b, input) in
                   if isValid(b,pos) then (nextAB(p), place(p,b,pos))
                                     else (p, b)

loop : (Player, Board) -> (Player, Board)
loop(p,b) = while not(gameOver(b)) do tryMove(p,b)

play : (Player, Board) -> Result
play(a,b) = outcome(loop(a,b))
